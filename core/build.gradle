plugins {
    id "org.jetbrains.kotlin.multiplatform"
    id("com.android.library")
    id("kotlin-android-extensions")
}
repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/kotlin/kotlin-eap/"
    }
}
group 'com.koushikdutta.scratch'
version '0.0.1'
def conscryptVersion = '2.5.0'

apply plugin: 'maven-publish'

kotlin {
    jvm()
//    js {
//        browser {
//        }
//        nodejs {
//        }
//    }
    android()

    compileKotlinJvm {
        kotlinOptions {
        }
        kotlinOptions.freeCompilerArgs = [
                "-Xallow-result-return-type",
                "-Xallow-kotlin-package",
                "-Xmulti-platform",
                "-Xuse-experimental=kotlin.Experimental",
                "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes",
                "-Xuse-experimental=kotlin.ExperimentalStdlibApi"
        ]
    }

    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64
    macosX64("native") {
        compilations.main.cinterops {
            uv {
                compilerOpts "-I./src/nativeInterop/cinterop/libuv/include"
            }
            crypto {
                compilerOpts "-I./src/nativeInterop/cinterop/openssl/include"
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                api project(':scratch:buffer')
                api project(':scratch:atomic')
                api project(':scratch:async')
                api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')

                compileOnly group: 'org.conscrypt', name: 'conscrypt-openjdk-uber', version: conscryptVersion

                // this is only necessary to get compilation working for self signed certificates. dependency isn't added.
                compileOnly group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.60'
                compileOnly group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.60'
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')

                implementation group: 'org.conscrypt', name: 'conscrypt-openjdk-uber', version: conscryptVersion
                implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.60'
                implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.60'
            }
        }
        androidMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')

                compileOnly group: 'org.conscrypt', name: 'conscrypt-android', version: conscryptVersion

                // this is only necessary to get compilation working for self signed certificates. dependency isn't added.
                compileOnly group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.60'
                compileOnly group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.60'
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')

                implementation 'androidx.test.ext:junit:1.1.2'
                implementation 'androidx.test.espresso:espresso-core:3.3.0'

                implementation group: 'org.conscrypt', name: 'conscrypt-android', version: conscryptVersion
                implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.60'
                implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.60'
            }
        }
//        jsMain {
//            dependencies {
//                implementation kotlin('stdlib-js')
//            }
//            languageSettings {
//                useExperimentalAnnotation('kotlin.Experimental') // annotation FQ-name
//                useExperimentalAnnotation('kotlin.ExperimentalMultiplatform') // annotation FQ-name
//                useExperimentalAnnotation('kotlin.ExperimentalUnsignedTypes') // annotation FQ-name
//                useExperimentalAnnotation('kotlin.ExperimentalStdlibApi') // annotation FQ-name
//            }
//        }
//        jsTest {
//            dependencies {
//                implementation kotlin('test-js')
//            }
//        }
        nativeMain {
        }
        nativeTest {
        }
    }
}

android {
    sourceSets.main.manifest.srcFile("src/androidMain/AndroidManifest.xml")
    sourceSets.main.java.srcDirs += "../core/src/jvmMain/kotlin"
    sourceSets.androidTest.java.srcDirs += "../core/src/jvmTest/kotlin"

    compileSdkVersion 30
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}
